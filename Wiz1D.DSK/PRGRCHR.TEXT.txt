 .PROC PRGRCHR,1 ; 1 PARAMETER    ; P010020,1
;
;        CALL:  PRGRCHR( ACHAR);
;
;               ACHAR IS A PACKED ARRAY[ 0..7] OF 0..255;  (SEE TCHRIMAG)
;
;        PURPOSE:  PRINT A CHARACTER TO THE HI RES SCREEN (7 X 8 PIXELS)
;
;        USES:  $579 IS LINE POSITION ON SCREEN
;               $4F9 IS HORIZONTAL POSTION ON SCREEN
;
 .DEF L0998, L0999
;
 PLA          ; PULL RETURN
 STA 00
 PLA
 STA 01
;
 PLA          ; 02 := CHARACTER TO DISPLAY  (PACKED ARRY[ 0..7] OF 0..255)
 STA 02
 PLA
 STA 03
;
 LDA 01       ; PUSH RETURN
 PHA
 LDA 00
 PHA
;
 CLD
 CLC
 LDA 00579    ; LINE POSITION
 ASL A        ; ADJUST SINCE L5998 AND L5999 HAVE 2 BYTES PER POINTER
 TAY
 LDA L5998,Y  ; LEFT EDGE OF SCREEN (TOP PIXEL OF CHARACTER)
 ADC 004F9    ; ADD HORIZONTAL POSITION ON SCREEN
 STA 04       ; SET 04.05 TO HI RES LOCATION FOR CHARACTER
 LDA L5999,Y
 STA 05
 LDX #08      ; EACH CHARACTER IS 8 PIXELS HIGH
 LDY #00
;
L597E
;
;             ; PLACE A ROW OF PIXELS ON THE SCREEN FOR THE CHARACTER
;
 LDA @2,Y     ; GET 7 PIXELS OF THE CHARACTER
 STA @4,Y     ; PUT 7 PIXELS ON THE SCREEN
 INC 05       ; ADVANCE 1 ROW OF PIXELS ON THE SCREEN
 INC 05
 INC 05
 INC 05
 CLC
 INC 02       ; INCREMENT TO NEXT ROW OF CHARACTER PIXELS
 BNE L5991
 INC 03
L5991
 DEX
 BNE L597E    ; DO ANOTHER ROW UNTIL 8 ARE PLACED ON SCREEN.
              ; 8 ROWS FOR 1 CHARACTER HAVE BEEN PLACED ON THE SCREEN.
 INC 004F9    ; ADVANCE HORIZONTAL POSITION
 RTS
;
;
;             ; TABLE OF 24 POINTERS TO LEFT EDGE OF HI RES SCREEN
;             ; EACH PTR REPRESENTS ONE "CHARACTER" ROW OF 8 PIXELS.
;
L0998
L5998
 .BYTE 0
L0999
L5999
 .BYTE 020, 080, 020, 000, 021, 080, 021, 000, 022, 080, 022, 000, 023, 080
 .BYTE 023, 028, 020, 0A8, 020, 028, 021, 0A8, 021, 028, 022, 0A8, 022, 028
 .BYTE 023, 0A8, 023, 050, 020, 0D0, 020, 050, 021, 0D0, 021, 050, 022, 0D0
 .BYTE 022, 050, 023, 0D0, 023
 .END
