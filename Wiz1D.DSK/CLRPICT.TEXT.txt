 .PROC CLRPICT,4 ;   P01001A,4 ; 4 PARAMETERS
;
 .REF L5AE4, L5B33
;  REFERENCE HI RES SCREEN MEMORY POINTERS (LEFT EDGE) START WITH LINE 5.
;
;
;  CLEAR "PICTURE" AREA OF SCREEN (82 X 79 PIXELS)
;
;      -OR-
;
;  SET VALID RECTANGLE COORDINATES
;
;
 PLA       ; POP RETURN
 STA 00
 PLA
 STA 01
;
 PLA
 STA 06    ; 06 := PARAM 4
 PLA
 LDA 06
 CMP #064  ; PARAMETER 4 = 100?
 BEQ L5BF5 ;   YES, L5BF5    (CLEAR PICTURE AREA)
 CMP #065  ; PARAMETER 4 = 101?
 BEQ L5BC3 ;   YES, L5BC3    (FOR WIZARDRY I IT IS NEVER 101)
;
;            $7F9, $779, $6F9, $679 ARE SLOT #1 RAM SCRATCHPAD!?
;
;
;            SET UP VALID RECTANGLE DRAWING REGION (BOUNDARIES)
;
;
 STA 007F9 ; $7F9 = PARAM 4  Y UPPER BOUNDARY (ALWAYS 79)
 PLA
 STA 00779 ; $779 = PARAM 3  X UPPER BOUNDARY
 PLA
 PLA
 STA 006F9 ; $6F9 = PARAM 2  Y LOWER BOUNDARY (ALWAYS 0)
 PLA
 PLA
 STA 00679 ; $679 = PARAM 1  X LOWER BOUNDARY
 PLA
;
 LDA 01    ; PUSH RETURN
 PHA
 LDA 00
 PHA
 RTS
;
;
;

L5BC3 ;      PARAM 4 SET TO 101  (NEVER EXECUTED)
 PLA
 STA 06
 PLA
 STA 07
 PLA
 PLA
 PLA
 PLA
 LDA 01
 PHA
 LDA 00
 PHA
 CLC
 CLD
 LDA #04E
 STA 04
L5BD9
 JSR L5C0E
 DEC 04
 CMP #040
 BNE L5BD9
L5BE2
 JSR L5C34
 DEC 04
 CMP #0D
 BNE L5BE2
L5BEB
 JSR L5C0E
 DEC 04
 BNE L5BEB
 JMP L5C0E
;
;
; CLEAR PICTURE DISPLAY AREA
;
; 79 LINES X 82 COLUMNS OF PIXELS ON THE HI RES SCREEN.
;
;   START WITH LINE 5 (ZERO BASED) AND END WITH LINE 83 (ZERO BASED).
;   START WITH COLUMN 4 (ZERO BASED) AND END WITH COLUMN 85 (ZERO BASED).
;
L5BF5     ;  PARAM 4 = 100.
 PLA      ;  THROW AWAY ALL PARAMETERS.
 PLA
 PLA
 PLA
 PLA
 PLA
 LDA 01   ; PUSH RETURN ADDRESS
 PHA
 LDA 00
 PHA
;
 CLC
 CLD
 LDA #04E ; EXECUTE L5C0E (CLEAR LINE) 79 TIMES
 STA 04
L5C07
 JSR L5C0E
 DEC 04
 BNE L5C07
;
;              CLEAR 82 HORIZONTAL PIXELS (MAINTAIN FIRST 4 PIXELS ON LINE)
;
L5C0E
 LDX 04      ; INDEX INTO HI-RES LINE PTRS.  LINES 5 TO 83. (DECIMAL, BASE 0)
 LDA L5AE4,X ; START AT BOTTOM OF SCREEN AND WORK UPWARDS
 STA 02
 LDA L5B33,X
 STA 03      ; $02.03 HAVE ADDRESS OF LEFT EDGE OF HI-RES SCREEN
;
 LDX #0B     ; CLEAR 11 FULL BYTES (7 X 11 = 77 SCREEN PIXELS)
 LDY #00
 LDA #0F     ; MAINTAIN FIRST 4 LEFTMOST PIXELS ON THIS LINE,
 AND @2,Y    ; BUT CLEAR THE OTHER 3 PIXELS IN THIS BYTE
 STA @2,Y
 INY
 LDA #00     ; CLEAR 7 DISPLAYABLE PIXELS FOR EACH BYTE OF HI RES MEMORY
;
L5C27
 STA @2,Y
 INY
 DEX
 BNE L5C27   ; CLEAR 11 FULL BYTES (7 X 11 = 77 SCREEN PIXELS)
;
 LDA #0FC    ; IN NEXT BYTE, CLEAR FIRST 2 LEFTMOST PIXELS BUT MAINTAIN
 AND @2,Y    ; OTHER 5 DISPLAYABLE PIXELS.
 STA @2,Y    ;    3 + 77 + 2 = 82 PIXELS
 RTS
;
;
;
L5C34 ;       PART OF L5BC3 CODE.  NEVER EXECUTED.
 LDX 04
 LDA L5AE4,X
 STA 02
 LDA L5B33,X
 STA 03
 LDX #0B
 LDY #00
 LDA #0F
 AND @2,Y
 STA @2,Y
 INY
L5C4B
 LDA @6,Y
 STA @2,Y
 INY
 DEX
 BNE L5C4B
 LDA #0FC
 AND @2,Y
 STA @2,Y
 CLC
 LDA 06
 ADC #0A
 STA 06
 LDA 07
 ADC #00
 STA 07
 RTS
 .END
