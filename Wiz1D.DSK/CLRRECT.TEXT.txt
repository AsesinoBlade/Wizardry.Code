 .PROC CLRRECT,4 ; 4 PARAMETERS   ;     P01001E,4 ;
;
 .REF L0998, L0999
;
;          ; CALL:  CLRRECT( X, Y, WIDTH, HEIGHT);
;
;          ; WIDTH = # OF COLUMNS (7 BITS PER COLUMN) TO CLEAR
;          ; HIEGHT = # OF LINES (8 BITS PER LINE) TO CLEAR
;
;          ; X, Y ARE UPPER LEFT CORNER OF RECTANGLE TO CLEAR
;          ; 0,0 IS UPPER LEFT ORIGIN
;
 CLC
 CLD
 PLA       ; PULL RETURN
 STA 00
 PLA
 STA 01
;
 PLA       ; 09 := PARM 4    HEIGHT (LINES)
 STA 09
 PLA       ; 07 := PARM 3    WIDTH  (COLS)
 PLA
 STA 07
 PLA
 PLA       ; 08 := PARM 2    Y
 STA 08
 PLA
 PLA       ; 06 := PARM 1    X
 STA 06
 PLA
;
 LDA 01    ; PUSH RETURN
 PHA
 LDA 00
 PHA
;
L5854
 JSR L585E ; CLEAR 1 "LINE" (8 PIXELS) ON THE HI RES SCREEN
 INC 08    ; INC TO NEXT LINE
 DEC 09    ; DEC LINES LEFT
 BNE L5854 ; DO ANOTHER LINE
 RTS
;
L585E
;            ; CLEAR 1 "LINE" (8 PIXELS) ON THE HI RES SCREEN.
;
 LDA 08      ; "LINE" NUMBER (8 PIXELS PER HI RES "LINE")
 ASL A       ; ADJUST.  2 BYTES PER POINTER IN L0998 TABLE
 TAY
 LDA L0998,Y ; 04.05 := PTR TO TOP OF "LINE" ON HI RES SCREEN
 STA 04      ;          THESE ARE LEFT EDGE OF SCREEN.
 LDA L0999,Y ;          EACH POINTER IN THIS TABLE OCCUPIES 2 BYTES.
 STA 05
;
 LDA #00
 JSR L5887   ; CLEAR HI RES ROW 1 FOR THIS "LINE" 
 JSR L5887   ; CLEAR HI RES ROW 2 FOR THIS "LINE"
 JSR L5887   ;
 JSR L5887   ;
 JSR L5887   ;
 JSR L5887   ;
 JSR L5887   ;
 JSR L5887   ; CLEAR HI RES ROW 8 FOR THIS "LINE"
 RTS
;
L5887
;            ; CLEAR 1 ROW OF PIXELS
;
 LDY 06      ; HORIZONTAL (X) COORDINATE
 LDX 07      ; NUMBER OF COLUMNS TO CLEAR
L588B
 STA @4,Y    ; CLEAR 7 PIXELS ON HI RES
 INY
 DEX
 BNE L588B   ; DO ANOTHER HI RES COLUMN
             ; DONE WITH THAT ROW
 INC 05      ; ADVANCE POINTER TO NEXT HI RES LINE
 INC 05
 INC 05
 INC 05
 RTS
 .END
